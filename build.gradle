apply plugin: 'com.android.library'
apply plugin: 'maven'

def GROUP_ID = 'fr.medes'
def ARTIFACT_ID = 'medes-android-utils'
def VERSION = '1.0.4-SNAPSHOT'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionName VERSION
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:preference-v7:26.1.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-net:commons-net:3.6'
}

// custom tasks for creating source/javadoc jars
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = GROUP_ID
            pom.artifactId = ARTIFACT_ID
            pom.version = VERSION
            pom.project {
                name ARTIFACT_ID
                packaging 'aar'
            }

            repository(url: "${NEXUS_URL}/content/repositories/releases") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            snapshotRepository(
                    url: "${NEXUS_URL}/content/repositories/snapshots") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
        }
    }
}
