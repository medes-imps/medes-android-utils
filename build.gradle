apply plugin: 'com.android.library'
apply plugin: 'maven'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.2'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 9
        versionCode 1
        versionName '1.0.4-SNAPSHOT'
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:preference-v7:25.3.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'commons-codec:commons-codec:1.10'
    compile 'commons-net:commons-net:3.6'
}

// custom tasks for creating source/javadoc jars
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {

            pom.groupId = 'fr.medes'
            pom.artifactId = 'medes-android-utils'
            pom.version = '1.0.4-SNAPSHOT'

            repository(url: "${NEXUS_URL}/content/repositories/releases") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }
            snapshotRepository(
                    url: "${NEXUS_URL}/content/repositories/snapshots") {
                authentication(userName: NEXUS_USERNAME, password: NEXUS_PASSWORD)
            }

            pom.project {
                name 'medes-android-utils'
                packaging 'aar'
            }
        }
    }
}
